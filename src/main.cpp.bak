#include <Arduino.h>
#include <Joystick.h>
#include <RotaryEncoder.h>

// --- Pin definitions ---
const uint8_t button1Pin = 2;
const uint8_t button2Pin = 3;

// Encoder pins
const uint8_t encoderPinA = 4;
const uint8_t encoderPinB = 5;

// --- Button to Joystick mapping ---
const uint8_t joyButton1 = 0;
const uint8_t joyButton2 = 1;
const uint8_t encoderBtnCW = 2;
const uint8_t encoderBtnCCW = 3;

Joystick_ Joystick(JOYSTICK_DEFAULT_REPORT_ID, JOYSTICK_TYPE_GAMEPAD,
                   4, 0,                 // Button Count, Hat Switch Count
                   false, false, false,  // X and Y, but no Z Axis
                   false, false, false,  // No Rx, Ry, or Rz
                   false, false,         // No rudder or throttle
                   false, false, false); // No accelerator, brake, or steering

RotaryEncoder encoder(encoderPinA, encoderPinB, RotaryEncoder::LatchMode::FOUR3);
int lastPos = 0;

void setup()
{
  pinMode(button1Pin, INPUT_PULLUP);
  pinMode(button2Pin, INPUT_PULLUP);
  pinMode(encoderPinA, INPUT_PULLUP);
  pinMode(encoderPinB, INPUT_PULLUP);

  // Initialize Joystick Library
  Joystick.begin();
  Serial.begin(115200);
}

void loop()
{

  // Read physical buttons
  Joystick.setButton(joyButton1, digitalRead(button1Pin) == LOW);
  Joystick.setButton(joyButton2, digitalRead(button2Pin) == LOW);

  encoder.tick();
  int newPos = encoder.getPosition();

  if (newPos != lastPos)
  {
    if (newPos > lastPos)
    {
      // Clockwise
      Joystick.setButton(encoderBtnCW, 1);
      delay(10);
      Joystick.setButton(encoderBtnCW, 0);
    }
    else
    {
      // Counter-clockwise
      Joystick.setButton(encoderBtnCCW, 1);
      delay(10);
      Joystick.setButton(encoderBtnCCW, 0);
    }
    lastPos = newPos;
  }
} // loop ()